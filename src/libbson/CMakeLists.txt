
#  .d8888b.           888
# d88P  Y88b          888
# Y88b.               888
#  "Y888b.    .d88b.  888888 888  888 88888b.
#     "Y88b. d8P  Y8b 888    888  888 888 "88b
#       "888 88888888 888    888  888 888  888
# Y88b  d88P Y8b.     Y88b.  Y88b 888 888 d88P
#  "Y8888P"   "Y8888   "Y888  "Y88888 88888P"
#                                     888
#                                     888
#                                     888

cmake_minimum_required (VERSION 3.15)

project (libbson
   LANGUAGES C
   # Inherit the version from mongo-c-driver
   VERSION "${PROJECT_VERSION}"
   DESCRIPTION "The libbson BSON serialization library"
)

# These values are inherited from the mongo-c-driver parent. These are named as to
# match the CMake variables generated by project().
set(libbson_VERSION_PRERELEASE ${mongo-c-driver_VERSION_PRERELEASE})
set(libbson_VERSION_FULL ${mongo-c-driver_VERSION_FULL})

# A tag attached to libbson artifacts, denoting the *major* API version, used to
# potentially co-locate additional future major versions.
set (BSON_API_VERSION 1.0)

if (APPLE)
   cmake_policy (SET CMP0042 OLD)
endif ()


#  .d8888b.           888    888    d8b
# d88P  Y88b          888    888    Y8P
# Y88b.               888    888
#  "Y888b.    .d88b.  888888 888888 888 88888b.   .d88b.  .d8888b
#     "Y88b. d8P  Y8b 888    888    888 888 "88b d88P"88b 88K
#       "888 88888888 888    888    888 888  888 888  888 "Y8888b.
# Y88b  d88P Y8b.     Y88b.  Y88b.  888 888  888 Y88b 888      X88
#  "Y8888P"   "Y8888   "Y888  "Y888 888 888  888  "Y88888  88888P'
#                                                     888
#                                                Y8b d88P
#                                                 "Y88P"

# libbson-specific configuration settings. Note that we inherit all settings
# from the parent!

mongo_setting(
   BSON_OUTPUT_BASENAME "Set the output basename for the libbson library"
   TYPE STRING
   DEFAULT VALUE "bson"
)
# Control over what components are installed:
mongo_bool_setting(
   ENABLE_STATIC_LIBBSON_INSTALL "Install static libbson"
   VISIBLE_IF ENABLE_STATIC)
mongo_bool_setting(
   ENABLE_SHARED_LIBBSON_INSTALL "Install shared libbson"
   VISIBLE_IF ENABLE_SHARED)


#  .d8888b.  888                        888
# d88P  Y88b 888                        888
# 888    888 888                        888
# 888        88888b.   .d88b.   .d8888b 888  888 .d8888b
# 888        888 "88b d8P  Y8b d88P"    888 .88P 88K
# 888    888 888  888 88888888 888      888888K  "Y8888b.
# Y88b  d88P 888  888 Y8b.     Y88b.    888 "88b      X88
#  "Y8888P"  888  888  "Y8888   "Y8888P 888  888  88888P'

# Configure-time platform checks. These start as regular CMake booleans, but are
# converted to 0/1 values (with mongo_bool01) so that they can be inserted into
# bson-config.h as preprocessor values. We cannot use #cmakedefine01, as we need
# to keep compatibility with an external Autotools-generated library configuration

include (CheckFunctionExists)
include (CheckIncludeFile)
include (CheckStructHasMember)
include (CheckSymbolExists)
include (TestBigEndian)
include (InstallRequiredSystemLibraries)
include (CheckIncludeFiles)

# See https://public.kitware.com/Bug/view.php?id=15659
check_symbol_exists (snprintf stdio.h BSON_HAVE_SNPRINTF)
mongo_bool01 (BSON_HAVE_SNPRINTF BSON_HAVE_SNPRINTF)
check_struct_has_member ("struct timespec" tv_sec time.h BSON_HAVE_TIMESPEC)
mongo_bool01 (BSON_HAVE_TIMESPEC BSON_HAVE_TIMESPEC)
check_symbol_exists (gmtime_r time.h BSON_HAVE_GMTIME_R)
mongo_bool01 (BSON_HAVE_GMTIME_R BSON_HAVE_GMTIME_R)
check_function_exists (rand_r BSON_HAVE_RAND_R)
mongo_bool01 (BSON_HAVE_RAND_R BSON_HAVE_RAND_R)
check_include_file (strings.h BSON_HAVE_STRINGS_H)
mongo_bool01 (BSON_HAVE_STRINGS_H BSON_HAVE_STRINGS_H)
check_symbol_exists (strlcpy string.h BSON_HAVE_STRLCPY)
mongo_bool01 (BSON_HAVE_STRLCPY BSON_HAVE_STRLCPY)
check_include_file (stdbool.h BSON_HAVE_STDBOOL_H)
mongo_bool01 (BSON_HAVE_STDBOOL_H BSON_HAVE_STDBOOL_H)
check_symbol_exists (clock_gettime time.h BSON_HAVE_CLOCK_GETTIME)
mongo_bool01 (BSON_HAVE_CLOCK_GETTIME BSON_HAVE_CLOCK_GETTIME)
check_symbol_exists (strnlen string.h BSON_HAVE_STRNLEN)
mongo_bool01 (BSON_HAVE_STRNLEN BSON_HAVE_STRNLEN)
test_big_endian (BSON_BIG_ENDIAN)

if (WIN32)
   set (BSON_OS 2)
else ()
   set (BSON_OS 1)
endif ()

if (BSON_BIG_ENDIAN)
   set (BSON_BYTE_ORDER 4321)
else ()
   set (BSON_BYTE_ORDER 1234)
endif ()

configure_file (
   "${PROJECT_SOURCE_DIR}/src/bson/bson-config.h.in"
   "${PROJECT_BINARY_DIR}/src/bson/bson-config.h"
)

configure_file (
   "${PROJECT_SOURCE_DIR}/src/bson/bson-version.h.in"
   "${PROJECT_BINARY_DIR}/src/bson/bson-version.h"
)

if (ENABLE_APPLE_FRAMEWORK)
   configure_file (
      "${PROJECT_SOURCE_DIR}/src/bson/modules/module.modulemap.in"
      "${PROJECT_BINARY_DIR}/src/bson/modules/module.modulemap"
   )
endif ()

# The subdirectory of the prefix for libbson headers:
set(BSON_HEADER_INSTALL_DIR "${CMAKE_INSTALL_INCLUDEDIR}/libbson-${BSON_API_VERSION}")
# The subdirectory of the prefix for the libbson CMake config-file packages and exported targets
set(BSON_TARGETS_INSTALL_DIR "${CMAKE_INSTALL_LIBDIR}/cmake/bson-${BSON_API_VERSION}")

# 8888888b.            .d888 d8b          d8b 888    d8b
# 888  "Y88b          d88P"  Y8P          Y8P 888    Y8P
# 888    888          888                     888
# 888    888  .d88b.  888888 888 88888b.  888 888888 888  .d88b.  88888b.  .d8888b
# 888    888 d8P  Y8b 888    888 888 "88b 888 888    888 d88""88b 888 "88b 88K
# 888    888 88888888 888    888 888  888 888 888    888 888  888 888  888 "Y8888b.
# 888  .d88P Y8b.     888    888 888  888 888 Y88b.  888 Y88..88P 888  888      X88
# 8888888P"   "Y8888  888    888 888  888 888  "Y888 888  "Y88P"  888  888  88888P'

include(MongoMultiTypeLibrary)
mongo_add_multitype_library(
   bson
   COLLECT_SOURCES "src/*.c" "src/*.h"
   OUTPUT_NAME "bson-1.0"
   REQUIREMENTS
      COMPILE_DEFINITIONS
         PRIVATE
            # Tell headers that they are part of compilation:
            BSON_COMPILATION
            # Enable NaN parsing in jsonsl
            JSONSL_PARSE_NAN
      COMPILE_OPTIONS
         PRIVATE
            # Macro constant INFINITY triggers constant arithmetic overflow warnings in
            # VS 2013, but VS 2013 doesn't support inline warning suppression.
            # Remove once support for VS 2013 is dropped.
            $<$<AND:$<C_COMPILER_ID:MSVC>,$<VERSION_LESS:${MSVC_VERSION},1900>>:/wd4756>
   STATIC_REQUIREMENTS
      # Users of the static library need BSON_STATIC to inform the headers that
      # we want to statically link them.
      COMPILE_DEFINITIONS INTERFACE BSON_STATIC
   # Select header files that will be installed:
   INSTALL_HEADERS FILES_MATCHING
      PATTERN "*.h"
      PATTERN "*-private.h" EXCLUDE
      # Installed separately below:
      PATTERN "bson/forwarding" EXCLUDE
      # Don't generate an empty "modules" directory
      PATTERN "bson/modules" EXCLUDE
      # This one isn't ours:
      PATTERN "jsonsl" EXCLUDE
   HEADER_INSTALL_DESTINATION "${BSON_HEADER_INSTALL_DIR}"
   TARGET_INSTALL_DESTINATION "${BSON_TARGETS_INSTALL_DIR}"
)

# 8888888888                                          888
# 888                                                 888
# 888                                                 888
# 8888888    888  888  8888b.  88888b.d88b.  88888b.  888  .d88b.  .d8888b
# 888        `Y8bd8P'     "88b 888 "888 "88b 888 "88b 888 d8P  Y8b 88K
# 888          X88K   .d888888 888  888  888 888  888 888 88888888 "Y8888b.
# 888        .d8""8b. 888  888 888  888  888 888 d88P 888 Y8b.          X88
# 8888888888 888  888 "Y888888 888  888  888 88888P"  888  "Y8888   88888P'
#                                            888
#                                            888
#                                            888

function (add_example bin src)
   set (BSON_EXAMPLE_SOURCES ${PROJECT_SOURCE_DIR}/${src})
   add_executable (${bin} ${BSON_EXAMPLE_SOURCES})

   # Link against the shared lib like normal apps
   if(TARGET mongo::bson_shared)
      target_link_libraries (${bin} mongo::bson_shared)
   elseif(TARGET mogno::bson_static)
      target_link_libraries (${bin} mogno::bson_static)
   else()
      return()
   endif()
endfunction ()

if (ENABLE_EXAMPLES)
   add_example (bcon-col-view examples/bcon-col-view.c)
   add_example (bcon-speed examples/bcon-speed.c)
   add_example (bson-metrics examples/bson-metrics.c)
   if (NOT WIN32)
      target_link_libraries (bson-metrics m)
      add_example (bson-streaming-reader examples/bson-streaming-reader.c)
   endif ()
   add_example (bson-to-json examples/bson-to-json.c)
   add_example (bson-validate examples/bson-validate.c)
   add_example (json-to-bson examples/json-to-bson.c)
   add_example (bson-check-depth examples/bson-check-depth.c)
   add_example (creating examples/creating.c)
endif () # ENABLE_EXAMPLES


# 8888888                   888             888 888
#   888                     888             888 888
#   888                     888             888 888
#   888   88888b.  .d8888b  888888  8888b.  888 888
#   888   888 "88b 88K      888        "88b 888 888
#   888   888  888 "Y8888b. 888    .d888888 888 888
#   888   888  888      X88 Y88b.  888  888 888 888
# 8888888 888  888  88888P'  "Y888 "Y888888 888 888

# Most installation was already handled by mongo_add_multitype_library() above.
# We have a few loose ends to tie up here:

# Generate the pkg-config files for the targets that we want to install
if(ENABLE_BSON_STATIC_INSTALL AND TARGET bson_static)
   mongo_generate_pkg_config(bson_static FILENAME libbson-static-1.0.pc INSTALL)
endif()
if(ENABLE_BSON_SHARED_INSTALL AND TARGET bson_shared)
   mongo_generate_pkg_config(bson_shared FILENAME libbson-1.0.pc INSTALL)
endif()

# The forwarding header is for compatibility with `#include <bson.h>`, so it goes at the top-level:
install(FILES src/bson/forwarding/bson.h DESTINATION "${BSON_HEADER_INSTALL_DIR}")

if (ENABLE_APPLE_FRAMEWORK)
   install (
      FILES "${PROJECT_BINARY_DIR}/src/bson/modules/module.modulemap"
      DESTINATION "${CMAKE_INSTALL_BINDIR}/bson.framework/Modules/"
   )
endif ()

# Generate the config-file package
include (CMakePackageConfigHelpers)
write_basic_package_version_file (
   "${CMAKE_CURRENT_BINARY_DIR}/bson/bson-${BSON_API_VERSION}-config-version.cmake"
   VERSION ${BSON_VERSION}
   COMPATIBILITY AnyNewerVersion
)

configure_file (src/bson-config.cmake
   "${CMAKE_CURRENT_BINARY_DIR}/bson/bson-${BSON_API_VERSION}-config.cmake"
   COPYONLY
)

install (
   FILES
      "${CMAKE_CURRENT_BINARY_DIR}/bson/bson-${BSON_API_VERSION}-config.cmake"
      "${CMAKE_CURRENT_BINARY_DIR}/bson/bson-${BSON_API_VERSION}-config-version.cmake"
   DESTINATION
      ${BSON_TARGETS_INSTALL_DIR}
   COMPONENT
      Devel
)

# Install the base targets. (Other targets were installed by mongo_add_multitype_library)
install (EXPORT bson-targets
   NAMESPACE mongo::
   FILE bson-targets.cmake
   DESTINATION ${BSON_TARGETS_INSTALL_DIR}
)

include (LegacyPackage)
include (CPack)

# 8888888b.
# 888  "Y88b
# 888    888
# 888    888  .d88b.   .d8888b .d8888b
# 888    888 d88""88b d88P"    88K
# 888    888 888  888 888      "Y8888b.
# 888  .d88P Y88..88P Y88b.         X88
# 8888888P"   "Y88P"   "Y8888P  88888P'

if (ENABLE_MAN_PAGES OR ENABLE_HTML_DOCS)
   find_package (Sphinx REQUIRED)
   add_subdirectory (doc)
   add_custom_target (bson-doc
      ALL
      DEPENDS
      $<$<BOOL:${ENABLE_MAN_PAGES}>:bson-man>
      $<$<BOOL:${ENABLE_HTML_DOCS}>:bson-html>
   )
endif ()
